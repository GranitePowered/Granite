// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'org.spongepowered:spongegradle:0.8.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'de.sebastianboegl.gradle.plugins:shadow-log4j-transformer:1.0.1'
    }
}

// Apply shared implementation Gradle config
apply from: project(':SpongeCommon').file('gradle/implementation.gradle')
apply plugin: 'de.sebastianboegl.shadow.transformer.log4j'

version = "$minecraft.version-$apiSuffix-$buildNumber"

minecraft {
    tweakClass = 'org.spongepowered.server.launch.VanillaServerTweaker'

    atSource sourceSets.main
}
ats += 'vanilla_at.cfg'

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
    log4j
}

configurations.forgeGradleMcDeps {
    transitive = false
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.2'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'

    compile 'org.apache.logging.log4j:log4j-iostreams:2.8.1'
    runtime 'com.lmax:disruptor:3.4.2' // Async Logging

    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.5'

    // Terminal console
    compile 'net.minecrell:terminalconsoleappender:1.1.1'
    runtime 'net.java.dev.jna:jna:4.5.2'

    shadow 'org.ow2.asm:asm-all:5.2'

    log4j('org.apache.logging.log4j:log4j-core:2.8.1') {
        transitive = false
    }
}

// Set main class and classpath for Minecraft server and Launchwrapper
jar {
    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.server.launch.VersionCheckingMain',
                'Class-Path': 'minecraft_server.1.12.2.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar'
        )
    }
}

// Reobfuscate to SRG mappings
reobf.jar.mappingType = 'SEARGE'

task('buildCompactMappings', type: org.spongepowered.gradle.cmap.BuildCompactMappingsTask, dependsOn: ['extractMcpMappings', 'splitServerJar']) {
    minecraftJar = plugins[forgeGradlePlugin].delayedFile(net.minecraftforge.gradle.common.Constants.JAR_SERVER_PURE)
    srg = plugins[forgeGradlePlugin].delayedFile(net.minecraftforge.gradle.common.Constants.MCP_DATA_SRG)
    output = new File(temporaryDir, "mappings.cmap")
}

shadowJar {
    // Exclude development Log4j configuration
    exclude 'log4j2-test.xml'

    configurations += [project.configurations.shadow]

    from tasks.buildCompactMappings
    from({zipTree(project.configurations.log4j.resolve().first())}) {
        include 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
    }

    dependencies {
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        include dependency('com.typesafe:config')

        include dependency('org.apache.logging.log4j:log4j-iostreams')
        include dependency('com.lmax:disruptor')

        include dependency('net.minecrell:terminalconsoleappender')
        include dependency('org.jline:jline-terminal')
        include dependency('org.jline:jline-terminal-jna')
        include dependency('org.jline:jline-reader')
        include dependency('net.java.dev.jna:jna')

        include dependency('com.eclipsesource.minimal-json:minimal-json')
    }
}

test {
    systemProperty 'lwts.tweaker', 'org.spongepowered.server.launch.TestTweaker'
}

license {
    exclude 'net/minecraftforge/**'
}
